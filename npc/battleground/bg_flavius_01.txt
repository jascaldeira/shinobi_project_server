// ==============================================================================
// BattleGround System - Flavius 1
// ==============================================================================

// Registration NPC's
// *********************************************************************

bat_room,253,97,4	script	Registration::Fl1R_Guillaume	418,{
	end;
OnInit:
	waitingroom "Sala de Espera (6 jogadores)",6,"Flavius_BG1::OnGuillaumeJoin",1;
	end;
OnEnterBG:
	set $@FlaviusBG1_id1, waitingroom2bg("bat_b01",390,10,"Flavius_BG1::OnGuillaumeQuit","");
setarray $BGid3[0],$@arenamembers[0], $@arenamembers[1], $@arenamembers[2], $@arenamembers[3], $@arenamembers[4],$@arenamembers[5];
	end;
}

bat_room,253,97,4	script	Retorno::Fl1R_Guillaume3	418,{
	for( set .i, 0; .i < getarraysize($BGid3); set .i, .i + 1 )
		if(getcharid(3) == $BGid3[.i]) warp "bat_b02",311,224;

	for( set .i, 0; .i < getarraysize($BGid4); set .i, .i + 1 )
		if(getcharid(3) == $BGid4[.i]) warp "bat_b02",87,75;
	end;
}

bat_room,250,74,4	script	Retorno::Fl1R_Croix3	414,{
	for( set .i, 0; .i < getarraysize($BGid3); set .i, .i + 1 )
		if(getcharid(3) == $BGid3[.i]) warp "bat_b02",311,224;

	for( set .i, 0; .i < getarraysize($BGid4); set .i, .i + 1 )
		if(getcharid(3) == $BGid4[.i]) warp "bat_b02",87,75;
	end;
}

bat_room,253,74,0	script	Registration::Fl1R_Croix	414,{
	end;
OnInit:
	waitingroom "Sala de Espera (6 jogadores)",6,"Flavius_BG1::OnCroixJoin",1;
	end;
OnEnterBG:
	set $@FlaviusBG1_id2, waitingroom2bg("bat_b01",10,290,"Flavius_BG1::OnCroixQuit","");
setarray $BGid4[0],$@arenamembers[0], $@arenamembers[1], $@arenamembers[2], $@arenamembers[3], $@arenamembers[4],$@arenamembers[5];
	end;
}

// Battleground Engine
// *********************************************************************

-	script	Flavius_BG1	-1,{
	end;

OnInit:
	disablenpc "Gerente A#fl1";
	disablenpc "Gerente B#fl1";
	disablenpc "Terapeuta#fl11";
	disablenpc "Terapeuta#fl12";
	end;

OnGuillaumeQuit:
OnCroixQuit:
	set BG_Delay_Tick, gettimetick(2) + 1200;
	end;

OnGuillaumeJoin:
OnCroixJoin:
	if( $@FlaviusBG1 == 0 )
		donpcevent "Flavius_BG1::OnReadyCheck";
	end;

OnReadyCheck:
	if( $@FlaviusBG1 )
		end;
	set .@Guillaume, getwaitingroomstate(0,"Fl1R_Guillaume");
	set .@Croix, getwaitingroomstate(0,"Fl1R_Croix");
	
	if( .@Guillaume < 6 || .@Croix < 6 )
	{
		mapannounce "bat_room","Battleground -- Shin'nyu [2] - Time A: " + .@Guillaume + "/6, Time B: " + .@Croix + "/6",1;
		end;
	}

	// BG Variables
	set $@FlaviusBG1, 1;
	set $@FlaviusBG1_Victory, 0;
	set .Guillaume_Score, 0;
	set .Guillaume_Loss, 0;
	set .Croix_Score, 0;
	set .Croix_Loss, 0;
	set .Match, 0;
	// Prepare NPC
	donpcevent "#gfl1_respawn::OnBGStart";
	donpcevent "#cfl1_respawn::OnBGStart";
	enablenpc "Terapeuta#fl11";
	enablenpc "Terapeuta#fl12";
	disablenpc "Gerente A#fl1";
	disablenpc "Gerente B#fl1";
	// Build and Warp Teams
	donpcevent "Fl1R_Guillaume::OnEnterBG";
	donpcevent "Fl1R_Croix::OnEnterBG";
	announce "Battleground -- Shin'nyu [2] iniciou!",0;
	initnpctimer;
	// Start Match!!

OnRoundStart:
	sleep 2000;
	if( $@FlaviusBG1 != 1 ) end;

	areapercentheal "bat_b01",382,2,397,17,100,100;
	areapercentheal "bat_b01",2,282,17,297,100,100;
	bg_warp $@FlaviusBG1_id1,"bat_b01",311,224;
	bg_warp $@FlaviusBG1_id2,"bat_b01",87,75;

	sleep 2000;
	if( $@FlaviusBG1 != 1 ) end;

	set .Match, .Match + 1;
	// Crystal Spawn
	set .Guillaume_Crystal, bg_monster($@FlaviusBG1_id1,"bat_b01",328,150,"Cristal de Chakra A",1914,"Flavius_BG1::OnGuillaumeBreak");
	setwall "bat_b01",327,149,2,6,0,"gui_wall1-1";
	setwall "bat_b01",329,149,2,0,0,"gui_wall1-2";
	setwall "bat_b01",329,151,2,2,0,"gui_wall1-3";
	setwall "bat_b01",327,151,2,4,0,"gui_wall1-4";

	set .Croix_Crystal, bg_monster($@FlaviusBG1_id2,"bat_b01",62,150,"Cristal de Chakra B",1915,"Flavius_BG1::OnCroixBreak");
	setwall "bat_b01",61,149,2,6,0,"cro_wall1-1";
	setwall "bat_b01",63,149,2,0,0,"cro_wall1-2";
	setwall "bat_b01",63,151,2,2,0,"cro_wall1-3";
	setwall "bat_b01",61,151,2,4,0,"cro_wall1-4";

	// Guardian Spawns
	bg_monster $@FlaviusBG1_id1,"bat_b01",328,160,"Guardião",1949,"Flavius_BG1::OnGuiGuardian";
	bg_monster $@FlaviusBG1_id1,"bat_b01",328,140,"Guardião",1950,"Flavius_BG1::OnGuiGuardian";
	set .Guillaume_Guardian, 2;
	bg_monster $@FlaviusBG1_id2,"bat_b01",62,160,"Guardião",1949,"Flavius_BG1::OnCroGuardian";
	bg_monster $@FlaviusBG1_id2,"bat_b01",61,140,"Guardião",1950,"Flavius_BG1::OnCroGuardian";
	set .Croix_Guardian, 2;
	// Announces
	mapannounce "bat_b01","[BG] A batalha Shin'nyu [2], rodada [ " + .Match + " ], teve início!!",1;
	end;

OnRoundStop:
	// Remove Monsters
	killmonster "bat_b01","Flavius_BG1::OnGuiGuardian";
	killmonster "bat_b01","Flavius_BG1::OnCroGuardian";
	delwall "gui_wall1-1";
	delwall "gui_wall1-2";
	delwall "gui_wall1-3";
	delwall "gui_wall1-4";
	killmonster "bat_b01","Flavius_BG1::OnGuillaumeBreak";
	delwall "cro_wall1-1";
	delwall "cro_wall1-2";
	delwall "cro_wall1-3";
	delwall "cro_wall1-4";
	killmonster "bat_b01","Flavius_BG1::OnCroixBreak";
	end;

OnGuiGuardian:
	if( set(.Guillaume_Guardian, .Guillaume_Guardian - 1) <= 0 )
	{
		delwall "gui_wall1-1";
		delwall "gui_wall1-2";
		delwall "gui_wall1-3";
		delwall "gui_wall1-4";
		mapannounce "bat_b01","[BG] O cristal da equipe A está vulnerável!",1;
	}
	end;

OnCroGuardian:
	if( set(.Croix_Guardian, .Croix_Guardian - 1) <= 0 )
	{
		delwall "cro_wall1-1";
		delwall "cro_wall1-2";
		delwall "cro_wall1-3";
		delwall "cro_wall1-4";
		mapannounce "bat_b01","[BG] O cristal da equipe B está vulnerável!",1;
	}
	end;

OnGuillaumeBreak:
	donpcevent "Flavius_BG1::OnRoundStop";
	set .Guillaume_Loss, .Guillaume_Loss + 1;
	if( set(.Croix_Score, .Croix_Score + 1) < 2 )
	{
		bg_updatescore "bat_b01",.Guillaume_Score,.Croix_Score;
		mapannounce "bat_b01","[BG] O cristal da equipe A foi destruído!",1;
		donpcevent "Flavius_BG1::OnRoundStart";
	}
	else
	{
		bg_updatescore "bat_b01",.Guillaume_Score,.Croix_Score;
		mapannounce "bat_b01","[BG] A equipe B venceu a batalha Shin'nyu!",1;
		set $@FlaviusBG1_Victory, 2;
		donpcevent "Flavius_BG1::OnMatchEnd";
	}
	end;

OnCroixBreak:
	donpcevent "Flavius_BG1::OnRoundStop";
	set .Croix_Loss, .Croix_Loss + 1;
	if( set(.Guillaume_Score, .Guillaume_Score + 1) < 2 )
	{
		bg_updatescore "bat_b01",.Guillaume_Score,.Croix_Score;
		mapannounce "bat_b01","[BG] O cristal da equipe B foi destruído!",1;
		donpcevent "Flavius_BG1::OnRoundStart";
	}
	else
	{
		bg_updatescore "bat_b01",.Guillaume_Score,.Croix_Score;
		mapannounce "bat_b01","[BG] A equipe A venceu a batalha Shin'nyu!",1;
		set $@FlaviusBG1_Victory, 1;
		donpcevent "Flavius_BG1::OnMatchEnd";
	}
	end;

OnTimer2400000:
	mapannounce "bat_b01","[BG] A batalha Shin'nyu irá terminar em 5 minutos",1;
	end;
OnTimer2640000:
	mapannounce "bat_b01","[BG] A batalha Shin'nyu irá terminar em 1 minuto",1;
	end;

OnTimer2700000:
	if( .Croix_Score > .Guillaume_Score )
	{
		mapannounce "bat_b01","[BG] A equipe B venceu a batalha Shin'nyu!",1;
		set $@FlaviusBG1_Victory, 2;
	}
	else if( .Croix_Score < .Guillaume_Score )
	{
		mapannounce "bat_b01","[BG] A equipe A venceu a batalha Shin'nyu!",1;
		set $@FlaviusBG1_Victory, 1;
	}
	else
	{
		mapannounce "bat_b01","[BG] A batalha de Shin'nyu acabou. O tempo se esgotou, a batalha terminou empatada",1;
		set $@FlaviusBG1_Victory, 3;
	}

OnMatchEnd:
	cleararray $BGid3[0],0,getarraysize($BGid3);
	cleararray $BGid4[0],0,getarraysize($BGid4);
	set $@FlaviusBG1, 2;
	stopnpctimer;
	donpcevent "#gfl1_respawn::OnBGStop";
	donpcevent "#cfl1_respawn::OnBGStop";
	disablenpc "Terapeuta#fl11";
	disablenpc "Terapeuta#fl12";
	enablenpc "Gerente A#fl1";
	enablenpc "Gerente B#fl1";
	sleep 2000;
	bg_warp $@FlaviusBG1_id1,"bat_b01",390,10;
	bg_warp $@FlaviusBG1_id2,"bat_b01",10,290;
	sleep 3000;
	mapannounce "bat_b01","[BG] A batalha Shin'nyu irá fechar em 1 minuto!",1;
	initnpctimer;
	end;

OnTimer30000:
	if( $@FlaviusBG1 == 2 )
		mapannounce "bat_b01","[BG] A batalha Shin'nyu irá fechar em 30 segundos",1;
	end;
OnTimer50000:
	if( $@FlaviusBG1 == 2 )
		mapannounce "bat_b01","[BG] A batalha Shin'nyu irá fechar em 10 segundos",1;
	end;

OnTimer60000:
	if( $@FlaviusBG1 != 2 )
		end;
OnReset:
	stopnpctimer;
	donpcevent "Flavius_BG1::OnRoundStop";
	set .Guillaume_Score, 0;
	set .Guillaume_Crystal, 0;
	set .Guillaume_Loss, 0;
	set .Croix_Score, 0;
	set .Croix_Crystal, 0;
	set .Croix_Loss, 0;
	set .Match, 0;
	set $@FlaviusBG1_Victory, 0;
	// NPC's
	disablenpc "Gerente A#fl1";
	disablenpc "Gerente B#fl1";
	disablenpc "Terapeuta#fl11";
	disablenpc "Terapeuta#fl12";

	if( $@FlaviusBG1_id1 ) { bg_destroy $@FlaviusBG1_id1; set $@FlaviusBG1_id1, 0; }
	if( $@FlaviusBG1_id2 ) { bg_destroy $@FlaviusBG1_id2; set $@FlaviusBG1_id2, 0; }
	sleep 1000;
	cleararray $BGid3[0],0,getarraysize($BGid3);
	cleararray $BGid4[0],0,getarraysize($BGid4);
	mapwarp "bat_b01","kakashi",84,145;
	sleep 2000;
	maprespawnguildid "bat_b01",0,3; // Just in case someone else
	sleep 2000;
	bg_updatescore "bat_b01",0,0;
	set $@FlaviusBG1, 0;
	donpcevent "Flavius_BG1::OnReadyCheck";
	end;

OnPCDieEvent:
if(strcharinfo(3) != "bat_b01") end;
sleep2(2000);
if( $@FlaviusBG1 != 1 ) end;
if(@bgid == 1) warp "bat_b01",390,13;
if(@bgid == 2) warp "bat_b01",10,293;
end;

}
	
	
bat_b01,311,224,3	script	Guillaume1#Register	-1,1,1,{
end;
OnTouch_:
	if(@bgid) end;
	set @bgid, 1;
	end;
}

bat_b01,87,75,3	script	Croix1#Register	-1,1,1,{
end;
OnTouch_:
	if(@bgid) end;
	set @bgid, 2;
	end;
}

// Battleground rewards
// *********************************************************************

bat_b01,390,13,5	script	Gerente A#fl1	419,{
	if( $@FlaviusBG1_Victory )
	{
		if( $@FlaviusBG1_Victory == Bat_Team )
		{ // Victory
			mes "[Gerente A]";
			mes "Grande vitória";
			mes "Vamos celebrar nosso triunfo glorioso";
			mes "" + strcharinfo(0) + ", receba esta recompensa pelo nossa vitória";
			close2;
			set .@Reward, 30;
		}
		else
		{ // 
			mes "[Gerente A]";
			mes "Nós perdemos, mas você se dedicou bastante pela equipe.";
			mes "Leve esta recompensa pelo seu tempo";
			close2;
			set .@Reward, 10;
		}

		set Flavius_BG_Tick, gettimetick(2) + 300;
		getitem 664, .@Reward;
		bg_leave;
		warp "kakashi",84,145;
		end;
	}
	end;
}

bat_b01,10,293,5	script	Gerente B#fl1	415,{
	if( $@FlaviusBG1_Victory )
	{
		if( $@FlaviusBG1_Victory == Bat_Team )
		{ // Victory
			mes "[Gerente B]";
			mes "Grande vitória";
			mes "Vamos celebrar nosso triunfo glorioso";
			mes "" + strcharinfo(0) + ", receba esta recompensa pelo nossa vitória";
			close2;
			set .@Reward, 30;
		}
		else
		{ // 
			mes "[Gerente B]";
			mes "Nós perdemos, mas você se dedicou bastante pela equipe.";
			mes "Leve esta recompensa pelo seu tempo";
			close2;
			set .@Reward, 10;
		}

		set Flavius_BG_Tick, gettimetick(2) + 300;
		getitem 664, .@Reward;
		bg_leave;
		warp "kakashi",84,145;
		end;
	}
	end;
}

// Battleground Therapist
// *********************************************************************

bat_b01,390,13,5	script	Terapeuta#fl12	95,{
	mes "[Terapeuta]";
	mes "Feche seus olhos, e respire fundo.";
	mes "Você será libertado de sua dor em 10 segundos";
	sleep2(8000);
	specialeffect2 312;
	sleep2(2000);
	areapercentheal "bat_b01",382,2,397,17,100,100;
	areawarp "bat_b01",382,2,397,17,"bat_b01",311,224;
	close;
}
bat_b01,10,293,5	script	Terapeuta#fl11	95,{
	mes "[Terapeuta]";
	mes "Feche seus olhos, e respire fundo.";
	mes "Você será libertado de sua dor em 10 segundos";
	sleep2(8000);
	specialeffect2 312;
	sleep2(2000);
	areapercentheal "bat_b01",2,282,17,297,100,100;
	areawarp "bat_b01",2,282,17,297,"bat_b01",87,75;
	close;
}

// Battleground Respawn
// *********************************************************************

bat_b01,390,10,0	script	#gfl1_respawn	139,{
	end;

OnBGStart:
	initnpctimer;
	end;

OnBGStop:
	stopnpctimer;
	end;

OnTimer24000:
	misceffect 83;
	end;

OnTimer25000:
	areapercentheal "bat_b01",382,2,397,17,100,100;
	areawarp "bat_b01",382,2,397,17,"bat_b01",311,224;
	initnpctimer;
	end;
}

bat_b01,10,290,0	script	#cfl1_respawn	139,{
	end;

OnBGStart:
	initnpctimer;
	end;

OnBGStop:
	stopnpctimer;
	end;

OnTimer24000:
	misceffect 83;
	end;

OnTimer25000:
	areapercentheal "bat_b01",2,282,17,297,100,100;
	areawarp "bat_b01",2,282,17,297,"bat_b01",87,75;
	initnpctimer;
	end;
}

// Flags
// *********************************************************************

//bat_b01,304,231,1	duplicate(Guillaume camp#bat)	Guillaume camp#bat1	973
//bat_b01,319,231,1	duplicate(Guillaume camp#bat)	Guillaume camp#bat2	973
//bat_b01,304,218,1	duplicate(Guillaume camp#bat)	Guillaume camp#bat3	973
//bat_b01,319,218,1	duplicate(Guillaume camp#bat)	Guillaume camp#bat4	973
//bat_b01,304,231,1	duplicate(Guillaume camp#bat)	Guillaume camp#bat5	973
//bat_b01,304,231,1	duplicate(Guillaume camp#bat)	Guillaume camp#bat6	973
//bat_b01,335,142,1	duplicate(Guillaume camp#bat)	Guillaume camp#bat7	973
//bat_b01,335,157,1	duplicate(Guillaume camp#bat)	Guillaume camp#bat8	973
//bat_b01,390,16,1	duplicate(Guillaume camp#bat)	Guillaume camp#bat9	973
//bat_b01,292,163,1	duplicate(Guillaume camp#bat)	Guillaume camp#bat19	973
//bat_b01,292,136,1	duplicate(Guillaume camp#bat)	Guillaume camp#bat20	973
//bat_b01,241,185,1	duplicate(Guillaume camp#bat)	Guillaume camp#bat21	973
//bat_b01,247,179,1	duplicate(Guillaume camp#bat)	Guillaume camp#bat22	973

//bat_b01,96,81,1	duplicate(Croix camp#bat)	Croix camp#bat1	974
//bat_b01,96,68,1	duplicate(Croix camp#bat)	Croix camp#bat2	974
//bat_b01,79,81,1	duplicate(Croix camp#bat)	Croix camp#bat3	974
//bat_b01,79,68,1	duplicate(Croix camp#bat)	Croix camp#bat4	974
//bat_b01,96,81,1	duplicate(Croix camp#bat)	Croix camp#bat5	974
//bat_b01,96,81,1	duplicate(Croix camp#bat)	Croix camp#bat6	974
//bat_b01,59,164,1	duplicate(Croix camp#bat)	Croix camp#bat7	974
//bat_b01,59,137,1	duplicate(Croix camp#bat)	Croix camp#bat8	974
//bat_b01,10,296,1	duplicate(Croix camp#bat)	Croix camp#bat9	974
//bat_b01,110,162,1	duplicate(Croix camp#bat)	Croix camp#bat18	974
//bat_b01,110,137,1	duplicate(Croix camp#bat)	Croix camp#bat19	974
//bat_b01,152,120,1	duplicate(Croix camp#bat)	Croix camp#bat20	974
//bat_b01,158,114,1	duplicate(Croix camp#bat)	Croix camp#bat21	974

// MapFlags
// *********************************************************************

bat_b01	mapflag	battleground	2
bat_b01	mapflag	nomemo
bat_b01	mapflag	nosave	SavePoint
bat_b01	mapflag	noteleport
bat_b01	mapflag	nowarp
bat_b01	mapflag	nowarpto
bat_b01	mapflag	noreturn
bat_b01	mapflag	nobranch
bat_b01	mapflag	nopenalty

-	script	resetbgtime1	-1,{

OnMapLoadEvent:
if(strcharinfo(3) != "bat_room") end;
set @bgid, 0;
end;

}
