// ==============================================================================
// BattleGround System - Flavius 2
// ==============================================================================

// Registration NPC's
// *********************************************************************

bat_room,57,97,4	script	Registration::Fl2R_Guillaume	418,{
	end;
OnInit:
	waitingroom "Sala de Espera (6 jogadores)",6,"Flavius_BG2::OnGuillaumeJoin",1;
	end;
OnEnterBG:
	set $@FlaviusBG2_id1, waitingroom2bg("bat_b02",390,10,"Flavius_BG2::OnGuillaumeQuit","");
setarray $BGid1[0],$@arenamembers[0], $@arenamembers[1], $@arenamembers[2], $@arenamembers[3], $@arenamembers[4],$@arenamembers[5];
	end;
}

bat_room,61,97,4	script	Retorno::Fl2R_Guillaume2	418,{
	for( set .i, 0; .i < getarraysize($BGid1); set .i, .i + 1 )
		if(getcharid(3) == $BGid1[.i]) warp "bat_b02",311,224;

	for( set .i, 0; .i < getarraysize($BGid2); set .i, .i + 1 )
		if(getcharid(3) == $BGid2[.i]) warp "bat_b02",87,75;
	end;
}

bat_room,54,74,4	script	Retorno::Fl2R_Croix2	414,{
	for( set .i, 0; .i < getarraysize($BGid1); set .i, .i + 1 )
		if(getcharid(3) == $BGid1[.i]) warp "bat_b02",311,224;

	for( set .i, 0; .i < getarraysize($BGid2); set .i, .i + 1 )
		if(getcharid(3) == $BGid2[.i]) warp "bat_b02",87,75;
	end;
}

bat_room,57,74,0	script	Registration::Fl2R_Croix	414,{
	end;
OnInit:
	waitingroom "Sala de Espera (6 jogadores)",6,"Flavius_BG2::OnCroixJoin",1;
	end;
OnEnterBG:
	set $@FlaviusBG2_id2, waitingroom2bg("bat_b02",10,290,"Flavius_BG2::OnCroixQuit","");
setarray $BGid2[0],$@arenamembers[0], $@arenamembers[1], $@arenamembers[2], $@arenamembers[3], $@arenamembers[4],$@arenamembers[5];
	end;
}

// Battleground Engine
// *********************************************************************

-	script	Flavius_BG2	-1,{
	end;

OnInit:
	disablenpc "Gerente A#fl2";
	disablenpc "Gerente B#fl2";
	disablenpc "Terapeuta#fl21";
	disablenpc "Terapeuta#fl22";
	end;

OnGuillaumeQuit:
OnCroixQuit:
	set BG_Delay_Tick, gettimetick(2) + 1200;
	end;

OnGuillaumeJoin:
OnCroixJoin:
	if( $@FlaviusBG2 == 0 )
		donpcevent "Flavius_BG2::OnReadyCheck";	
	end;

OnReadyCheck:
	if( $@FlaviusBG2 )
		end;
	set .@Guillaume, getwaitingroomstate(0,"Fl2R_Guillaume");
	set .@Croix, getwaitingroomstate(0,"Fl2R_Croix");
	
	if( .@Guillaume < 6 || .@Croix < 6 )
	{
		mapannounce "bat_room","Battleground -- Shin'nyu - Time A: " + .@Guillaume + "/6, Time B: " + .@Croix + "/6",1;
		end;
	}

	// BG Variables
	set $@FlaviusBG2, 1;
	set $@FlaviusBG2_Victory, 0;
	set .Guillaume_Score, 0;
	set .Guillaume_Loss, 0;
	set .Croix_Score, 0;
	set .Croix_Loss, 0;
	set .Match, 0;
	// Prepare NPC
	donpcevent "#gfl2_respawn::OnBGStart";
	donpcevent "#cfl2_respawn::OnBGStart";
	enablenpc "Terapeuta#fl21";
	enablenpc "Terapeuta#fl22";
	disablenpc "Gerente A#fl2";
	disablenpc "Gerente B#fl2";
	// Build and Warp Teams
	donpcevent "Fl2R_Guillaume::OnEnterBG";
	donpcevent "Fl2R_Croix::OnEnterBG";
	announce "Battleground -- Shin'nyu iniciou!",0;
	initnpctimer;
	// Start Match!!

OnRoundStart:
	sleep 2000;
	if( $@FlaviusBG2 != 1 ) end;

	areapercentheal "bat_b02",382,2,397,17,100,100;
	areapercentheal "bat_b02",2,282,17,297,100,100;
	bg_warp $@FlaviusBG2_id1,"bat_b02",311,224;
	bg_warp $@FlaviusBG2_id2,"bat_b02",87,75;

	sleep 2000;
	if( $@FlaviusBG2 != 1 ) end;

	set .Match, .Match + 1;
	// Crystal Spawn
	set .Guillaume_Crystal, bg_monster($@FlaviusBG2_id1,"bat_b02",328,150,"Cristal de Chakra A",1914,"Flavius_BG2::OnGuillaumeBreak");
	setwall "bat_b02",327,149,2,6,0,"gui_wall2-1";
	setwall "bat_b02",329,149,2,0,0,"gui_wall2-2";
	setwall "bat_b02",329,151,2,2,0,"gui_wall2-3";
	setwall "bat_b02",327,151,2,4,0,"gui_wall2-4";

	set .Croix_Crystal, bg_monster($@FlaviusBG2_id2,"bat_b02",62,150,"Cristal de Chakra A",1915,"Flavius_BG2::OnCroixBreak");
	setwall "bat_b02",61,149,2,6,0,"cro_wall2-1";
	setwall "bat_b02",63,149,2,0,0,"cro_wall2-2";
	setwall "bat_b02",63,151,2,2,0,"cro_wall2-3";
	setwall "bat_b02",61,151,2,4,0,"cro_wall2-4";

	// Guardian Spawns
	bg_monster $@FlaviusBG2_id1,"bat_b02",328,160,"Guardião",1949,"Flavius_BG2::OnGuiGuardian";
	bg_monster $@FlaviusBG2_id1,"bat_b02",328,140,"Guardião",1950,"Flavius_BG2::OnGuiGuardian";
	set .Guillaume_Guardian, 2;
	bg_monster $@FlaviusBG2_id2,"bat_b02",62,160,"Guardião",1949,"Flavius_BG2::OnCroGuardian";
	bg_monster $@FlaviusBG2_id2,"bat_b02",61,140,"Guardião",1950,"Flavius_BG2::OnCroGuardian";
	set .Croix_Guardian, 2;
	// Announces
	mapannounce "bat_b02","[BG] A batalha Shin'nyu, rodada [ " + .Match + " ], teve início!!",1;
	end;

OnRoundStop:
	// Remove Monsters
	killmonster "bat_b02","Flavius_BG2::OnGuiGuardian";
	killmonster "bat_b02","Flavius_BG2::OnCroGuardian";
	delwall "gui_wall2-1";
	delwall "gui_wall2-2";
	delwall "gui_wall2-3";
	delwall "gui_wall2-4";
	killmonster "bat_b02","Flavius_BG2::OnGuillaumeBreak";
	delwall "cro_wall2-1";
	delwall "cro_wall2-2";
	delwall "cro_wall2-3";
	delwall "cro_wall2-4";
	killmonster "bat_b02","Flavius_BG2::OnCroixBreak";
	end;

OnGuiGuardian:
	if( set(.Guillaume_Guardian, .Guillaume_Guardian - 1) <= 0 )
	{
		delwall "gui_wall2-1";
		delwall "gui_wall2-2";
		delwall "gui_wall2-3";
		delwall "gui_wall2-4";
		mapannounce "bat_b02","[BG] O cristal da equipe A está vulnerável!",1;
	}
	end;

OnCroGuardian:
	if( set(.Croix_Guardian, .Croix_Guardian - 1) <= 0 )
	{
		delwall "cro_wall2-1";
		delwall "cro_wall2-2";
		delwall "cro_wall2-3";
		delwall "cro_wall2-4";
		mapannounce "bat_b02","[BG] O cristal da equipe B está vulnerável!",1;
	}
	end;

OnGuillaumeBreak:
	donpcevent "Flavius_BG2::OnRoundStop";
	set .Guillaume_Loss, .Guillaume_Loss + 1;
	if( set(.Croix_Score, .Croix_Score + 1) < 2 )
	{
		bg_updatescore "bat_b02",.Guillaume_Score,.Croix_Score;
		mapannounce "bat_b02","[BG] O cristal da equipe A foi destruído!",1;
		donpcevent "Flavius_BG2::OnRoundStart";
	}
	else
	{
		bg_updatescore "bat_b02",.Guillaume_Score,.Croix_Score;
		mapannounce "bat_b02","[BG] A equipe B venceu a batalha Shin'nyu!",1;
		set $@FlaviusBG2_Victory, 2;
		donpcevent "Flavius_BG2::OnMatchEnd";
	}
	end;

OnCroixBreak:
	donpcevent "Flavius_BG2::OnRoundStop";
	set .Croix_Loss, .Croix_Loss + 1;
	if( set(.Guillaume_Score, .Guillaume_Score + 1) < 2 )
	{
		bg_updatescore "bat_b02",.Guillaume_Score,.Croix_Score;
		mapannounce "bat_b02","[BG] O cristal da equipe B foi destruído!",1;
		donpcevent "Flavius_BG2::OnRoundStart";
	}
	else
	{
		bg_updatescore "bat_b02",.Guillaume_Score,.Croix_Score;
		mapannounce "bat_b02","[BG] A equipe A venceu a batalha Shin'nyu!",1;
		set $@FlaviusBG2_Victory, 1;
		donpcevent "Flavius_BG2::OnMatchEnd";
	}
	end;

OnTimer2400000:
	mapannounce "bat_b02","[BG] A batalha Shin'nyu irá terminar em 5 minutos",1;
	end;
OnTimer2640000:
	mapannounce "bat_b02","[BG] A batalha Shin'nyu irá terminar em 1 minuto",1;
	end;

OnTimer2700000:
	if( .Croix_Score > .Guillaume_Score )
	{
		mapannounce "bat_b02","[BG] A equipe B venceu a batalha Shin'nyu!",1;
		set $@FlaviusBG2_Victory, 2;
	}
	else if( .Croix_Score < .Guillaume_Score )
	{
		mapannounce "bat_b02","[BG] A equipe A venceu a batalha Shin'nyu!",1;
		set $@FlaviusBG2_Victory, 1;
	}
	else
	{
		mapannounce "bat_b02","[BG] A batalha de Shin'nyu acabou. O tempo se esgotou, a batalha terminou empatada",1;
		set $@FlaviusBG2_Victory, 3;
	}

OnMatchEnd:
	cleararray $BGid1[0],0,getarraysize($BGid1);
	cleararray $BGid2[0],0,getarraysize($BGid2);
	set $@FlaviusBG2, 2;
	stopnpctimer;
	donpcevent "#gfl2_respawn::OnBGStop";
	donpcevent "#cfl2_respawn::OnBGStop";
	disablenpc "Terapeuta#fl21";
	disablenpc "Terapeuta#fl22";
	enablenpc "Gerente A#fl2";
	enablenpc "Gerente B#fl2";
	sleep 2000;
	bg_warp $@FlaviusBG2_id1,"bat_b02",390,10;
	bg_warp $@FlaviusBG2_id2,"bat_b02",10,290;
	sleep 3000;
	mapannounce "bat_b02","[BG] A batalha Shin'nyu irá fechar em 1 minuto!",1;
	initnpctimer;
	end;

OnTimer30000:
	if( $@FlaviusBG2 == 2 )
		mapannounce "bat_b02","[BG] A batalha Shin'nyu irá fechar em 30 segundos",1;
	end;
OnTimer50000:
	if( $@FlaviusBG2 == 2 )
		mapannounce "bat_b02","[BG] A batalha Shin'nyu irá fechar em 10 segundos",1;
	end;

OnTimer60000:
	if( $@FlaviusBG2 != 2 )
		end;
OnReset:
	stopnpctimer;
	donpcevent "Flavius_BG2::OnRoundStop";
	set .Guillaume_Score, 0;
	set .Guillaume_Crystal, 0;
	set .Guillaume_Loss, 0;
	set .Croix_Score, 0;
	set .Croix_Crystal, 0;
	set .Croix_Loss, 0;
	set .Match, 0;
	set $@FlaviusBG2_Victory, 0;
	// NPC's
	disablenpc "Gerente A#fl2";
	disablenpc "Gerente B#fl2";
	disablenpc "Terapeuta#fl21";
	disablenpc "Terapeuta#fl22";

	if( $@FlaviusBG2_id1 ) { bg_destroy $@FlaviusBG2_id1; set $@FlaviusBG2_id1, 0; }
	if( $@FlaviusBG2_id2 ) { bg_destroy $@FlaviusBG2_id2; set $@FlaviusBG2_id2, 0; }
	sleep 1000;
	cleararray $BGid1[0],0,getarraysize($BGid1);
	cleararray $BGid2[0],0,getarraysize($BGid2);
	mapwarp "bat_b02","kakashi",84,145;
	sleep 2000;
	maprespawnguildid "bat_b02",0,3; // Just in case someone else
	sleep 2000;
	bg_updatescore "bat_b02",0,0;
	set $@FlaviusBG2, 0;
	donpcevent "Flavius_BG2::OnReadyCheck";
	end;
	
OnPCDieEvent:
if(strcharinfo(3) != "bat_b02") end;
sleep2(2000);
if( $@FlaviusBG2 != 1 ) end;
if(@bgid == 1) warp "bat_b02",390,13;
if(@bgid == 2) warp "bat_b02",10,293;
end;

}
	
	
bat_b02,311,224,3	script	Guillaume2#Register	-1,1,1,{
end;
OnTouch_:
	if(@bgid) end;
	set @bgid, 1;
	end;
}

bat_b02,87,75,3	script	Croix2#Register	-1,1,1,{
end;
OnTouch_:
	if(@bgid) end;
	set @bgid, 2;
	end;
}

// Battleground rewards
// *********************************************************************

bat_b02,390,13,5	script	Gerente A#fl2	419,{
	if( $@FlaviusBG2_Victory )
	{
		if( $@FlaviusBG2_Victory == 1 )
		{ // Victory
			mes "[Gerente A]";
			mes "Grande vitória";
			mes "Vamos celebrar nosso triunfo glorioso";
			mes "" + strcharinfo(0) + ", receba esta recompensa pelo nossa vitória";
			close2;
			set .@Reward, 30;
		}
		else
		{ // 
			mes "[Gerente A]";
			mes "Nós perdemos, mas você se dedicou bastante pela equipe.";
			mes "Leve esta recompensa pelo seu tempo";
			close2;
			set .@Reward, 10;
		}

		set Flavius_BG_Tick, gettimetick(2) + 300;
		getitem 664, .@Reward;
		bg_leave;
		warp "kakashi",84,145;
		end;
	}
	end;
}

bat_b02,10,293,5	script	Gerente B#fl2	415,{
	if( $@FlaviusBG2_Victory )
	{
		if( $@FlaviusBG2_Victory == 2 )
		{ // Victory
			mes "[Gerente B]";
			mes "Grande vitória";
			mes "Vamos celebrar nosso triunfo glorioso";
			mes "" + strcharinfo(0) + ", receba esta recompensa pelo nossa vitória";
			close2;
			set .@Reward, 30;
		}
		else
		{ // 
			mes "[Gerente B]";
			mes "Nós perdemos, mas você se dedicou bastante pela equipe.";
			mes "Leve esta recompensa pelo seu tempo";
			close2;
			set .@Reward, 10;
		}

		set Flavius_BG_Tick, gettimetick(2) + 300;
		getitem 664, .@Reward;
		bg_leave;
		warp "kakashi",84,145;
		end;
	}
	end;
}

// Battleground Therapist
// *********************************************************************

bat_b02,390,15,5	script	Terapeuta#fl22	95,{
	mes "[Terapeuta]";
	mes "Feche seus olhos, e respire fundo.";
	mes "Você será libertado de sua dor em 10 segundos";
	sleep2(8000);
	specialeffect2 312;
	sleep2(2000);
	areapercentheal "bat_b02",382,2,397,17,100,100;
	areawarp "bat_b02",382,2,397,17,"bat_b02",311,224;
	close;
}

bat_b02,10,295,5	script	Terapeuta#fl21	95,{
	mes "[Terapeuta]";
	mes "Feche seus olhos, e respire fundo.";
	mes "Você será libertado de sua dor em 10 segundos";
	sleep2(8000);
	specialeffect2 312;
	sleep2(2000);
	areapercentheal "bat_b02",2,282,17,297,100,100;
	areawarp "bat_b02",2,282,17,297,"bat_b02",87,75;
	close;
}

// Battleground Respawn
// *********************************************************************

bat_b02,390,10,0	script	#gfl2_respawn	139,{
	end;

OnBGStart:
	initnpctimer;
	end;

OnBGStop:
	stopnpctimer;
	end;

OnTimer24000:
	misceffect 83;
	end;

OnTimer25000:
	areapercentheal "bat_b02",382,2,397,17,100,100;
	areawarp "bat_b02",382,2,397,17,"bat_b02",311,224;
	initnpctimer;
	end;
}

bat_b02,10,290,0	script	#cfl2_respawn	139,{
	end;

OnBGStart:
	initnpctimer;
	end;

OnBGStop:
	stopnpctimer;
	end;

OnTimer24000:
	misceffect 83;
	end;

OnTimer25000:
	areapercentheal "bat_b02",2,282,17,297,100,100;
	areawarp "bat_b02",2,282,17,297,"bat_b02",87,75;
	initnpctimer;
	end;
}

// Flags
// *********************************************************************

//bat_b02,304,231,1	duplicate(Guillaume camp#bat)	Guillaume camp#bat32	973
//bat_b02,319,231,1	duplicate(Guillaume camp#bat)	Guillaume camp#bat33	973
//bat_b02,304,218,1	duplicate(Guillaume camp#bat)	Guillaume camp#bat34	973
//bat_b02,319,218,1	duplicate(Guillaume camp#bat)	Guillaume camp#bat35	973
//bat_b02,304,231,1	duplicate(Guillaume camp#bat)	Guillaume camp#bat36	973
//bat_b02,304,231,1	duplicate(Guillaume camp#bat)	Guillaume camp#bat37	973
//bat_b02,335,142,1	duplicate(Guillaume camp#bat)	Guillaume camp#bat38	973
//bat_b02,335,157,1	duplicate(Guillaume camp#bat)	Guillaume camp#bat39	973
//bat_b02,390,16,1	duplicate(Guillaume camp#bat)	Guillaume camp#bat40	973
//bat_b02,292,163,1	duplicate(Guillaume camp#bat)	Guillaume camp#bat41	973
//bat_b02,292,136,1	duplicate(Guillaume camp#bat)	Guillaume camp#bat42	973
//bat_b02,241,185,1	duplicate(Guillaume camp#bat)	Guillaume camp#bat43	973
//bat_b02,247,179,1	duplicate(Guillaume camp#bat)	Guillaume camp#bat44	973

//bat_b02,96,81,1	duplicate(Croix camp#bat)	Croix camp#bat30	974
//bat_b02,96,68,1	duplicate(Croix camp#bat)	Croix camp#bat31	974
//bat_b02,79,81,1	duplicate(Croix camp#bat)	Croix camp#bat32	974
//bat_b02,79,68,1	duplicate(Croix camp#bat)	Croix camp#bat33	974
//bat_b02,96,81,1	duplicate(Croix camp#bat)	Croix camp#bat34	974
//bat_b02,96,81,1	duplicate(Croix camp#bat)	Croix camp#bat35	974
//bat_b02,59,164,1	duplicate(Croix camp#bat)	Croix camp#bat36	974
//bat_b02,59,137,1	duplicate(Croix camp#bat)	Croix camp#bat37	974
//bat_b02,10,296,1	duplicate(Croix camp#bat)	Croix camp#bat38	974
//bat_b02,110,162,1	duplicate(Croix camp#bat)	Croix camp#bat39	974
//bat_b02,110,137,1	duplicate(Croix camp#bat)	Croix camp#bat40	974
//bat_b02,152,120,1	duplicate(Croix camp#bat)	Croix camp#bat41	974
//bat_b02,158,114,1	duplicate(Croix camp#bat)	Croix camp#bat42	974

// MapFlags
// *********************************************************************

bat_b02	mapflag	battleground
bat_b02	mapflag	battleground	2
bat_b02	mapflag	nomemo
bat_b02	mapflag	nosave	SavePoint
bat_b02	mapflag	noteleport
bat_b02	mapflag	nowarp
bat_b02	mapflag	nowarpto
bat_b02	mapflag	noreturn
bat_b02	mapflag	nobranch
bat_b02	mapflag	nopenalty
bat_room	mapflag	pvp	off

-	script	resetbgtime	-1,{

OnMapLoadEvent:
if(strcharinfo(3) != "bat_room") end;
set @bgid, 0;
end;

}